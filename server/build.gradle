buildscript {
    dependencies {
        classpath 'org.gretty:gretty:2.3.1'
    }
}

apply plugin: 'war'

apply plugin: 'netflix.ospackage-tomcat'

apply plugin: 'org.gretty'

// Gretty allows us to run tomcat from the command line using the "tomcatRun" task.
Properties development_properties = new Properties()
development_properties.load(new FileInputStream(file("src/main/resources/laptop.properties")))
gretty {
    contextPath = '/'
    servletContainer = 'tomcat8'
    systemProperties = development_properties
    scanDirs = ['**/src/main/webapp/**']
    scanDependencies = true
    // More properties can be found here:
    // http://akhikhl.github.io/gretty-doc/Gretty-configuration.html
}

// The ezconfig configuration kicks in when we build our debian package and deploy in AWS.
// It contains all the OS env variable configurations and anything else that tunes our boxes and
// tomcat server.
ospackage {
    packageName = 'vmstransformer'
    requires('nflx-ezconfig')
    requires('nflx-base-www')
    requires('nflx-ulog-tomcat')
    postInstall('mkdir /etc/nflx-ulog/; echo "export ulog.stream=\"vms\"" > /etc/nflx-ulog/ulog.conf')
    // A future base AMI release will make this the default
    postInstall('system-jdk --set zulu-8-amd64')
}

dependencies {
    // Governator wires up all of our dependencies in dependency injection fashion.
    compile 'com.netflix.governator:governator-core'
    compile 'com.netflix.governator:governator-servlet'
    compile 'com.netflix.governator:governator-jersey'
    // We don't want this to be packaged with the rest. It's only for local runs of our main().
    providedCompile ('com.netflix.governator:governator-jetty') { transitive=false }
    // Same for the servlet libs.
    providedCompile 'org.eclipse.jetty:jetty-servlet'
    providedCompile 'javax.servlet:javax.servlet-api'

    compile 'netflix:nf-eventbus-guice'
    compile 'netflix:nf-eventbus'
    compile 'netflix:nf-eventbus-core'

    // Runtime lifecycle modules bring in all the essential Netflix platform dependencies.
    compile 'com.netflix.runtime:runtime-lifecycle'
    compile 'netflix:base-server'

    // Cassandra Aeneas driver
    compile "com.netflix.aeneas:aeneas-core"

    // VMS Transformer
    compile project(':vmstransformer-common-interfaces')
    compile project(':vmstransformer-io')
    compile project(':vmstransformer-business-logic')
    compile project(':vmstransformer-publish-workflow')
    compile 'com.netflix.vms.logging:vmslogging-slf4j:latest.release'
    compile 'netflix:vms-hollow-generated-notemplate:latest.release'

    compile 'netflix:cinder-core:latest.release'
    compile 'netflix:cinder-lifecycle:latest.release'

    // httpclient
    compile 'org.apache.httpcomponents:httpclient'

    testCompile 'junit:junit'
    testCompile 'org.mockito:mockito-all:1.3'
    testCompile 'org.assertj:assertj-core'
    testCompile 'com.netflix.hollow:hollow-diff-ui:2.+'
    testCompile 'com.netflix.hollow:hollow-explorer-ui:2.+'
    testCompile 'com.netflix.hollow:hollow-jsonadapter:2.+'

    // Cassandra Aeneas unit test kit.
    testCompile "com.netflix.aeneas:aeneas-test"
    testCompile 'com.netflix.governator:governator-test-junit'
    testCompile "netflix:videoresolutiontypelibrary:latest.release"

    testCompile 'com.netflix.hollow:hollow-diff-ui:2.+'
    testCompile 'org.eclipse.jetty:jetty-server'
}

facets {
    tools {
        parentSourceSet = 'test'
    }
}

// Required exclusions to work around a bug in gretty: log4j-over-slf4j is accidentally included
configurations.grettyRunnerTomcat7 {
    exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}
configurations.grettyRunnerTomcat8 {
    exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}

war {
  into('WEB-INF/classes') {
    from 'src/main/java'
  }
}
